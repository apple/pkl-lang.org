amends "@pkl.impl.ghactions/PklCI.pkl"

import "@gha/actions/Setup.pkl"
import "@gha/actions/Common.pkl"
import "@gha/Context.pkl"
import "@gha/Workflow.pkl"

local baseJob: Workflow.Job = new {
  `runs-on` = "ubuntu-latest"
  steps {
    new Common.Checkout {
      with {
        `fetch-depth` = 0
      }
    }
    new Setup.Java {
      name = "Setup Java"
      with {
        `java-version` = "21"
        distribution = "temurin"
        cache = "gradle"
      }
    }
    new {
      name = "Setup Rust"
      uses = "actions-rust-lang/setup-rust-toolchain@063a3b947b5c5bf7d5f87076c3e5e9784b776aa8"
      with {
        ["cache-workspaces"] = "pkl-html-highlighter"
      }
    }
    new {
      name = "Configure cargo"
      // language=bash
      run = """
        cat <<EOF > ~/.cargo/config
        [net]
        git-fetch-with-cli = true
        EOF
        """
    }
  }
}

local buildAndTest: Workflow = new {
  jobs {
    ["build-and-test"] = (baseJob) {
      steps {
        new {
          name = "Run Gradle Tests"
          // language=bash
          run = #"""
            ./gradlew \
              --console=plain \
              pklHtmlHighlighter

            ./gradlew \
              --console=plain \
              npmInstall buildRemoteSite
            """#
        }
      }
    }
  }
}

prb = buildAndTest

build = buildAndTest

main {
  on {
    workflow_dispatch {
      inputs {
        ["source_run"] {
          type = "string"
          required = false
          description = "The source GitHub Action workflow run triggering this build"
        }
      }
    }
  }
  jobs {
    ["publish"] = (baseJob) {
      permissions {
        contents = "write"
      }
      steps = new {
        new {
          name = "Triggered by"
          `if` = "inputs.source_run != null"
          run = """
            echo "Triggered by workflow in repo: ${{ inputs.source_run }}" >> $GITHUB_STEP_SUMMARY
            """
        }
        ...super.steps
        new Common.Checkout {
          with {
            `fetch-depth` = 0
            `persist-credentials` = true
          }
        }
        new {
          name = "Run publish"
          // language=bash
          run = #"""
            ./gradlew \
              --console=plain \
              pklHtmlHighlighter

            ./gradlew \
              --console=plain \
              npmInstall buildRemoteSite publishRemoteSite
            """#
        }
      }
    }
  }
}
